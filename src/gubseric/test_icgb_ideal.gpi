#if (exists("tag")) print sprintf("tag is defined as = %s", tag)
tag = "icgb"
aratio = 0.85
fwidth  = 3.375*2
fheight = fwidth*aratio
set terminal wxt size 1000, 1200 enhanced dashed
#set terminal epslatex size fwidth, 2.0*fheight font "phv" 10 standalone
#outputname = sprintf("test_%s_ideal.tex", tag)
#set output outputname
set style line 1 lt 1 lw 3 pt 6 ps 1 lc rgb "dark-red"
set style line 2 lt 2 lw 3 pt 1 ps 1 lc rgb "dark-red"
set style line 3 lt 3 lw 3 pt 3 ps 2 lc rgb  "black" 
set style line 4 lt 2 lw 3 pt 5 ps 2 lc rgb "orange"
set style line 6 lt 7 lw 5 pt 3 ps 2 lc rgb  "medium-blue" 
#set object 2 rect from screen 0,0 to screen 1,1 fs empty  back`

set multiplot layout 3,2
set view map
unset key
set palette defined ( 0 "#000090",\
                      1 "#000fff",\
                      2 "#0090ff",\
                      3 "#0fffee",\
                      4 "#90ff70",\
                      5 "#ffee00",\
                      6 "#ff7000",\
                      7 "#ee0000",\
                      8 "#7f0000")
set size square
set xlabel "$x$"
set ylabel "$y$"
set cblabel "$e$"
set title "Initial energy density"
splot [-5:5][-5:5] tag."_2dslice_xy_0000.out" every 1 using 2:3:5 index 0 w pm3d
set title "Final energy density"
splot [-5:5][-5:5] tag."_2dslice_xy_0000.out" every 1 using 2:3:5 index 5 w pm3d

q=0.1
e0=1.0
E0=5.0
S0=E0*4/3.
energy(tau,xx,yy) = e0/tau**(4/3.)*(2*q)**(8/3.0)/(1+2*q**2*(tau**2+xx**2+yy**2)+q**4*(tau**2-xx**2-yy**2)**2)**(4/3.)
temp(tau,xx,yy) = 4/3.*energy(tau,xx,yy)**(1/4.)*E0**(3/4.)/S0
rho(tt,xx,yy) = asinh(-(1-q**2*(tt**2 - xx**2+yy**2))/(2*q*tt))
vr(tt,xx,yy) = 2*q**2*tt*sqrt(xx**2+yy**2)/(1+q**2*(tt**2+xx**2+yy**2))
set xrange [0:5]
set cblabel "$\\tau$"
set autoscale y
set key
set xlabel "$x$"
set ylabel "$e\\tau^4$"
set title "$e\\tau^4$ evolution"
plot \
tag."_1dslice_x_0000_0000.out"  using 2:($5*$1**4):1 t "hydro"  w l ls 1 lc palette,\
tag."2_1dslice_x_0000_0000.out" using 2:($5*$1**4):1 t "icstep" w l ls 3
    
set yrange [0:1]
set ylabel "$v_\\perp$"
set title "radial velocity"
plot \
tag."_1dslice_x_0000_0000.out"  using 2:(sqrt($12*$12+$13*$13)/$11):1 t "hydro"  w l ls 1 lc palette,\
tag."2_1dslice_x_0000_0000.out" using 2:(sqrt($12*$12+$13*$13)/$11):1 t "icstep" w l ls 3 

set autoscale x
set autoscale y
set ylabel "$s\\tau^3$"
set title "entropy at the origin"
set xlabel "$t$"
plot \
tag."_point_0000_0000_0000.out" using 1:($8*$1**3) t "hydro" w l ls 1, \
tag."2_point_0000_0000_0000.out"  using 1:($8*$1**3) t "icstep"  w l ls 3

set ylabel "$e\\tau^4$"
set title "energy at the origin"
set xlabel "$t$"
plot \
tag."_point_0000_0000_0000.out" using 1:($5*$1**4) t "hydro" w l ls 1, \
tag."2_point_0000_0000_0000.out"  using 1:($5*$1**4) t "icstep"  w l ls 3

#set title "entropy vs time"
#set xlabel "$\\tau$"
#set ylabel "$\\int s dx dy$"
#plot  \
#tag."_entropy_vs_time_z0000.out" using 1:2 t "hydro" w l ls 1,\
#tag."2_entropy_vs_time_z0000.out" using 1:2 t "ideal" w l ls 3

unset multiplot
